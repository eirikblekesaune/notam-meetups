# Meetup #1 2023, January 9th
This was the first meetup for our explorations of rhythms in SuperCollider.

## `Live 4 Life` spatial performance tool
@Xon77 shared a link to a spatial performance tool, `Live 4 Life`: [https://github.com/Xon77/Live4Life]

## `SuperClean`
@danielmkarlsson shared a link to his `SuperClean` project: [https://github.com/danielmkarlsson/SuperClean]

## The_emergent SuperCollider live streams
Camilla shared a link to their weekly Twitch stream: [https://www.twitch.tv/the_emergent]

## AntiBjorklund
@redFrik wrote a new class `AntiBjorklund`
```supercollider
	AntiBjorklund : Bjorklund {
		*new {|k, n|
			^this.fromArray([0].dup(k)++[1].dup(n-k));
		}
	}
	AntiPbjorklund : Pbjorklund {
		*new {|k, n, length= inf, offset= 0|
			^1-super.newCopyArgs(k, n, length, offset)
		}
	}
```
With testcode:
```supercollider
	//testcode...
  a= Pbjorklund(3, 8, 1).asStream;
	9.do{a.next.postln};
	b= AntiPbjorklund(3, 8, 1).asStream;
	9.do{b.next.postln};
	
	s.boot;
	Pbind(\dur, 0.2, \amp, Pbjorklund(11, 16, 1)).play;
	Pbind(\dur, 0.2, \amp, AntiPbjorklund(11, 16, 1)).play;

	Quarks.install("Bjorklund")
```

## Benjolin/Rungler patch
@nhthn shared a link to a Benjolin/Rungler patch he has made: [https://scsynth.org/t/another-benjolin-rungler-patch/6599]

## Linear feedback shift register
Connected to @nhthn Rungler patch above, @redFrik shared a patch for a 4-bit LFSR, using single sample feedback with Dbufrd/Dbufwr
```supercollider
	//4-bit Fibonacci LFSR as single sample feedback with dbufrd/dbufwr
	(
	a= {|rate= 4, iseed= 2r1000|
		var b, trig;
		var buf= LocalBuf(1);
		buf.set(iseed);
		trig= Impulse.ar(rate);
		b= Demand.ar(trig, 0, Dbufrd(buf));//read
		b= (b>>1)|((b&1).bitXor(b&2>>1)<<3);//modify
		Demand.ar(trig, 0, Dbufwr(b, buf));//write
		b.poll(trig);
		DC.ar(0);
	}.play;
	)
	a.set(\rate, 30)
	a.free
	(
	a= {|rate= 400, iseed= 2r1000, tap1= 1, tap2= 3, tap3= 5, length= 16|
		var l, b, trig, o;
		var buf= LocalBuf(1);
		buf.set(iseed);
		trig= Impulse.ar(rate);
		l= Demand.ar(trig, 0, Dbufrd(buf));//read
		b= l.bitXor(l>>tap1).bitXor(l>>tap2).bitXor(l>>tap3)&1;//modify
		l= (l>>1)|(b<<15);//lfsr
		Demand.ar(trig, 0, Dbufwr(l, buf));//write
		o= PulseCount.ar(Impulse.ar(rate*length), trig);//bits
		l>>o&1!2;//output
	}.play;
	)
	
	a.set(\rate, 300)
	a.set(\rate, 100)
	a.set(\length, 3)
	a.set(\length, 14)
	a.set(\tap1, 14.rand)
	a.set(\tap2, 14.rand)
	a.set(\tap3, 14.rand)
	a.set(\length, 32)
	a.set(\rate, 1000)
	a.free
```

## Moving tempo
@ffforces demonstrated using `Pseg` for getting gradual shifts in base tempo of rhythms played with `Pbind`:
```supercollider
(
x=(Pdef(\yamaha,
Pbind(
    \type, \midi,
    \midicmd, \noteOn,
    \midiout,MIDIOut.newByName("loopMIDI Port", "loopMIDI Port", 3).latency_(0);, //this is just for pc users
		\chan, 0,

		\root, -24,
		\degree, Pdefn(\degree),
		\dur, Pseg(Pseq([1/15,1/19,1/15,1/19,[1/2].choose,1/25,1/45,1/12],inf)*0.1, Pwhite(0.01,4, inf),\welch, inf),
	\stretch, 60/120*4,
		\legato, Prand([1, 1, 1, 1], inf),
\amp, 1,

));
)
)
Pdefn(\degree, Prand([0, 2, 4, 7, 9, 13], inf));
y=x.play;
y.stop;
//Pfsm can be used for finite state machines, here used for degrees:

(
Pdefn(\degree, Pfsm([
        #[0,1],
        0, #[0, 0, 1,3,5,2],
       7, #[2,0,1,1],
        2, #[0, 2],
	9, #[5,4,0,1,2],
	16, #[2,4,0],
        Pseq([0,0, 0, 7]), #[0, 3, 3,2],
	nil, nil], inf));
)

(
Pdefn(\degree, Pfsm([
        #[0,1],
        0, #[0, 0, 1,3,5,2],
       7, #[2,0,1,1,0,0,0],
        2, #[0, 2,0,0,0],
	9, #[5,4,0,1,2],
	16, #[2,4,0,0,0,0],
        Pseq([0,0, 0, 7]), #[0, 3, 3,2,0,0],
	nil, nil], inf));
)

(
Pdefn(\degree, Pfsm([
        #[0,1],
        0, #[0, 2],
       7, #[2],
        2, #[0, 2],
	9, #[0,2],
	16, #[0,2],
        Pseq([0,0, 0, 7]), #[0,1,2,3,4,5],
	nil, nil], inf));
)


y.stop;
y.play;
g=(Pdef(\yamahatoinenmelodia,



Pbind(


    \type, \midi,

    \midicmd, \noteOn,
    \midiout, mOut,
    \chan, 1,
	\dummy, Pfunc

	\scale, Pseq([Scale.minor], inf),
	\degree,
		(Pseq([-7, 0, 2, 4], inf)+Pseq([-4, 0, 0, 6, -2, 0, 0, 0, 0], inf)-2 )*Pseq([0.5,2, 1, 1,1], inf),
	\dur, 1/32,
	\stretch, 60/100*4,
		\legato, Pseq([1,1,1,1,1,1], inf),
	\amp, Pseq([1], inf);

)).quant_(60/100*4);
))

h=g.play;
h.stop;
```

## Parallel patterns
@ffforces tipped that you can use ['Plambda'](https://doc.sccode.org/Classes/Plambda.html) for paralell patterns.

### Using `Pseed` to sync `Pbind` streams
@danielmkarlsson showed am example of how can sync random values in paralell patterns using the `Pseed` class:
```supercollider
(
Pdef(0,
    Pbind(*[
        type: Pseed(0, Prand([\cln, \], inf)),
        snd: Prand([\mmd, \clp, \drm], inf),
        num: Prand([0, 3], inf),
        rel: 1/9,
        dur: 1 / Pseed(0, Pdup(Phprand(7, 15), Phprand(7, 15))),
        amp: 0.6,
    ])
).play;
Pdef(1,
    Pbind(*[
        type: Pseed(0, Prand([\cln, \], inf)),
        snd: \fmx,
        rps: Pexprand(9, 19),
        hr1: Pdup(Pkey(\rps) - Pwhite(0, 7), Pshuf((1 .. 4), inf)),
        hr2: Pdup(Pkey(\rps) + Pwhite(0, 7), Pshuf((1 .. 4), inf)),
        hr3: Pdup(Pkey(\rps) - Pwhite(0, 7), Pshuf((1 .. 4), inf)),
        hr4: Pdup(Pkey(\rps) + Pwhite(0, 7), Pshuf((1 .. 4), inf)),
        fdb: Pexprand(0.0001, 100.0),
        mi2: Pdup(Pkey(\rps) + Pwhite(0, 7), Pshuf((0.0001 .. 4.0), inf)),
        mi3: Pdup(Pkey(\rps) + Pwhite(0, 7), Pshuf((0.0001 .. 4.0), inf)),
        mi4: Pdup(Pkey(\rps) + Pwhite(0, 7), Pshuf((0.0001 .. 4.0), inf)),
        amp: Pexprand(0.5, 0.75),
        en1: Pdup(Pkey(\rps) + Pwhite(0, 7), Pexprand(0.0001, 0.555)),
        en2: Pdup(Pkey(\rps) + Pwhite(0, 7), Pkey(\en1) * Pexprand(0.2, 0.666)),
        en3: Pdup(Pkey(\rps) + Pwhite(0, 7), Pkey(\en1) * Pkey(\en2) / Pexprand(0.3, 0.777)),
        en4: Pdup(Pkey(\rps) + Pwhite(0, 7), Pkey(\en1) * Pkey(\en2) / Pkey(\en3) * Pexprand(0.4, 0.888)),
        cu1: Pdup(Pkey(\rps) + Pwhite(0, 7), Pwhite(0.25, 1.0)),
        cu2: Pdup(Pkey(\rps) + Pwhite(0, 7), Pwhite(0.25, 1.0)),
        cu3: Pdup(Pkey(\rps) + Pwhite(0, 7), Pwhite(0.25, 1.0)),
        cu4: Pdup(Pkey(\rps) + Pwhite(0, 7), Pwhite(0.25, 1.0)),
        rel: Plprand(1/9, 0.9),
        dur: 1/9,
        dur: 1 / Pseed(0, Pdup(Phprand(7, 15), Phprand(7, 15))),
        legato: 9,
    ])
).play
)
```

## The legendary `Pn(Plazy())`-trick
@danielmkarlsson showed a trick, handed down to to him by David GranstrÃ¸m. I expands rhythmic structures by adding a tail of duration values in this case.
```supercollider
// You need SuperClean to run this code: https://github.com/danielmkarlsson/SuperClean
(
var sequence = [5, 7, 9];
var originalSequence = sequence.copy;
Pdef(0,
    Pbind(*[
        type: \cln,
        snd: \drm,
        dur: 1 / Pn(Plazy {
            var value = exprand(4, 10);
            sequence = sequence ++ value;
            if (sequence.size > exprand(4, 10)) {
                sequence = originalSequence;
            };
            sequence.debug('seq');
            Pseq(sequence);
        }),
        amp: Prand([0.7, 1.0], inf),
        fed: Plprand(0.0, 2.0),
    ])
).play;
)
```


## Declaring note/duration lists for patterns
@redFrik showed an example for declaring `dur`/`freq` lists in a `Pbind`:
```supercollider
Pbind(
  #[dur, freq], Pseq([
    [0.2, 400],
    [0.3, 500],
    [0.4, 600],
    [Rest(0.6), nil],
    [0.7, 700],
    [0.8, 800],
    [0.9, [600, 700, 800]]
  ]).trace
).play;
)
```
## String rewriting, L-systems
@nthtn shared a link to a project he is working on with String Rewriting [https://nathan.ho.name/posts/sound-synthesis-with-l-systems/] , where the goal is to make a server side UGen for L-systems.

@redFrik shared a link to the [Prewrite](https://doc.sccode.org/Classes/Prewrite.html) that does a similar thing i `sclang`


